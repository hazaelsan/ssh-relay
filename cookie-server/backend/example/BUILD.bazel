load("@aspect_bazel_lib//lib:expand_template.bzl", "expand_template")
load("@aspect_bazel_lib//lib:transitions.bzl", "platform_transition_filegroup")
load("@rules_go//go:def.bzl", "go_binary", "go_library")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_image_index", "oci_load", "oci_push")
load("@rules_pkg//:pkg.bzl", "pkg_tar")

package(default_visibility = ["//cookie-server:__subpackages__"])

expand_template(
    name = "labels",
    out = "labels.txt",
    stamp_substitutions = {
        "0.0.0": "{{BUILD_EMBED_LABEL}}",
        "2001-02-03T04:05:06Z": "{{BUILD_TIMESTAMP_ISO8601}}",
    },
    template = [
        "org.opencontainers.image.version=0.0.0",
        "org.opencontainers.image.source=https://github.com/hazaelsan/ssh-relay/cookie-server/backend/example",
        "org.opencontainers.image.title=SSH Relay Cookie Server example backend",
        "org.opencontainers.image.description=SSH-over-WebSocket Cookie Server example backend.",
        "org.opencontainers.image.base.name=gcr.io/distroless/base-debian12:nonroot",
        "org.opencontainers.image.created=2001-02-03T04:05:06Z",
    ],
)

expand_template(
    name = "stamped_tags",
    out = "stamped.tags.txt",
    stamp_substitutions = {"0.0.0": "{{BUILD_EMBED_LABEL}}"},
    template = [
        "0.0.0",
        "latest",
    ],
)

go_binary(
    name = "example",
    embed = [":example_lib"],
)

go_library(
    name = "example_lib",
    srcs = ["example.go"],
    importpath = "github.com/hazaelsan/ssh-relay/cookie-server/backend/example",
    deps = [
        "//cookie-server/backend/example/proto/v1:config_go_proto",
        "//cookie-server/proto/v1:service_go_proto",
        "//tls",
        "@com_github_golang_glog//:glog",
        "@org_golang_google_grpc//:grpc",
        "@org_golang_google_grpc//credentials",
        "@org_golang_google_protobuf//encoding/prototext",
    ],
)

pkg_tar(
    name = "tar",
    srcs = [":example"],
)

oci_image(
    name = "image",
    base = "@distroless_nonroot",
    entrypoint = ["/example"],
    labels = ":labels",
    tars = [":tar"],
)

oci_image_index(
    name = "image_index",
    images = [":image"],
)

platform_transition_filegroup(
    name = "transitioned_image",
    srcs = [":image"],
    target_platform = select({
        "@platforms//cpu:arm64": "@rules_go//go/toolchain:linux_arm64",
        "@platforms//cpu:x86_64": "@rules_go//go/toolchain:linux_amd64",
    }),
)

oci_load(
    name = "load",
    image = ":transitioned_image",
    repo_tags = ["hazaelsan/cookie-server-example-backend:latest"],
)

oci_push(
    name = "push",
    image = ":image_index",
    remote_tags = ":stamped_tags",
    repository = "index.docker.io/hazaelsan/cookie-server-example-backend",
)
